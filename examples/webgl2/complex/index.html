<!DOCTYPE html>
<html>
<head>
  <title>[WIP] Testing Composite sample Using WebGL 2.0</title>
  <link rel="stylesheet" type="text/css" href="style.css">
  <script src="//code.jquery.com/jquery-3.4.0.js"></script>
  <script src="https://unpkg.com/gl-matrix@3.4.4/gl-matrix-min.js"></script>
</head>
<body>

<script id="vs" type="x-shader/x-vertex">#version 300 es
layout(location = 0) in vec3 aPosition;
layout(location = 1) in vec3 aNormal;
layout(location = 2) in vec2 aTexCoord;
layout(location = 3) in uvec4 aJoints;
layout(location = 4) in vec4 aWeights;

uniform mat4 uModelMatrix;
uniform mat4 uViewMatrix;
uniform mat4 uProjectionMatrix;
uniform mat3 uNormalMatrix;
uniform mat4 uJointMatrices[180];
uniform bool uHasSkinning;

out vec3 vNormal;
out vec2 vTexCoord;
out vec3 vPosition;

void main() {
    vec4 position = vec4(aPosition, 1.0);
    vec3 normal = aNormal;
    
    if (uHasSkinning) {
        mat4 skinMatrix = 
            aWeights.x * uJointMatrices[aJoints.x] +
            aWeights.y * uJointMatrices[aJoints.y] +
            aWeights.z * uJointMatrices[aJoints.z] +
            aWeights.w * uJointMatrices[aJoints.w];
        
        position = skinMatrix * position;
        normal = mat3(skinMatrix) * normal;
    }
    
    vec4 worldPosition = uModelMatrix * position;
    vPosition = worldPosition.xyz;
    vNormal = uNormalMatrix * normal;
    vTexCoord = aTexCoord;
    gl_Position = uProjectionMatrix * uViewMatrix * worldPosition;
}
</script>

<script id="fs" type="x-shader/x-fragment">#version 300 es
precision highp float;

in vec3 vNormal;
in vec2 vTexCoord;
in vec3 vPosition;

uniform sampler2D uTexture;
uniform bool uHasTexture;
uniform vec4 uBaseColor;
uniform vec3 uLightDir;

out vec4 fragColor;

void main() {
    // Flat shading: compute normal from surface derivatives
    vec3 normal = normalize(cross(dFdx(vPosition), dFdy(vPosition)));
    
    vec3 lightDir = normalize(uLightDir);
    
    float diff = max(dot(normal, lightDir), 0.0);
    float ambient = 0.3;
    float lighting = ambient + diff * 0.7;
    
    vec4 baseColor;
    if (uHasTexture) {
        baseColor = texture(uTexture, vTexCoord);
    } else {
        baseColor = uBaseColor;
    }

    vec3 finalColor = baseColor.rgb * lighting;
    finalColor = pow(finalColor, vec3(1.0 / 2.2));
    
    fragColor = vec4(finalColor, baseColor.a);
}
</script>

<!-- Skybox Vertex Shader -->
<script id="vs-skybox" type="x-shader/x-vertex">#version 300 es
layout(location = 0) in vec3 aPosition;

out vec3 vTexCoord;

uniform mat4 uProjectionMatrix;
uniform mat4 uViewMatrix;

void main() {
    vTexCoord = aPosition;
    
    vec4 pos = uProjectionMatrix * uViewMatrix * vec4(aPosition, 1.0);
    
    gl_Position = pos.xyww;
}
</script>

<!-- Skybox Fragment Shader -->
<script id="fs-skybox" type="x-shader/x-fragment">#version 300 es
precision highp float;

in vec3 vTexCoord;

uniform samplerCube uSkybox;

out vec4 fragColor;

void main() {
    fragColor = texture(uSkybox, vTexCoord);
}
</script>

<!-- Tire Track Vertex Shader -->
<script id="vs-track" type="x-shader/x-vertex">#version 300 es
layout(location = 0) in vec3 aPosition;

uniform mat4 uModelMatrix;
uniform mat4 uViewMatrix;
uniform mat4 uProjectionMatrix;

void main() {
    gl_Position = uProjectionMatrix * uViewMatrix * uModelMatrix * vec4(aPosition, 1.0);
}
</script>

<!-- Tire Track Fragment Shader -->
<script id="fs-track" type="x-shader/x-fragment">#version 300 es
precision highp float;

uniform vec4 uColor;

out vec4 fragColor;

void main() {
    fragColor = uColor;
}
</script>

<canvas id="c" width="465" height="465"></canvas>
<script type="module" src="index.js"></script>
</body>
</html>
